//HTTP: ram=HTTP2
MULTISET: allhttps=HTTP+;

HTTP:((namewrap=NAMEWRAP)?
    /curl/?
    urlwrap=URL
    (basic_auth_wrap=BASICAUTH)?
    (lines *= LINE)?
    (payload=PAYLOAD)?
    (output=TOFILE)?);

URL:
    ('-X'? method=METHODTYPE)? url=URLString SLASH?
;

NAMEWRAP:
    '@name' '(' name=DotString ')'
;

LINE: 
    (query=QUERY) | (header=HEADER)
;

HEADER: 
    'header' '(' key=DotString ',' value=DotString ')'
    | key=DotString ':' value=DotString
    | /-H|--header/  key=/'[\w-]+/ ':' value=/\S(.*\S)?'/ SLASH?
    | /-H|--header/  key=/"[\w-]+/ ':' value=/\S(.*\S)?"/ SLASH?
    | /-H|--header/  key=/[\w-]+/ ':' value=/\S(.*\S)?/
    | key=/'[\w-]+/ ':' value=/\S(.*\S)?'/
;

BASICAUTH:
    'basicauth' '(' username=DotString ',' password= DotString ')'
;

METHODTYPE2:
    "get" | "post" | "options"
    | "delete" | "connect" | "put"
    | "head" | "trace" | "patch"
    | "copy" | "link" | "unlink"
    | "purge" | "lock" | "unlock"
    | "propfind" | "view"
;


METHODTYPE: 
    "GET" | "POST" | "OPTIONS"
    | "DELETE" | "CONNECT" | "PUT"
    | "HEAD" | "TRACE" | "PATCH"
    | "COPY" | "LINK" | "UNLINK"
    | "PURGE" | "LOCK" | "UNLOCK"
    | "PROPFIND" | "VIEW"
;

QUERY: 
    'query' '(' key=DotString ',' value=DotString ')'
    | '?' '(' key=DotString ',' value=DotString ')'
    | '?' key=DotString ',' value=DotString
    | '?' key=DotString ':' value=DotString
    | '?' key=DotString '=' value=DotString
;

PAYLOAD: 
    (   'data' '(' data=STRING (',' type=STRING)? ','?  ')'
        | 'data' '(' multi=MULTILINE_STRING (',' type=STRING)? ','?  ')'
        | 'data' '(' datajson=JSON (',' type=STRING)? ','? ')'
        | /-d|--data/ data=STRING
        | 'urlencoded' '(' datajson=JSON (',' type=STRING)? ','? ')'
        | 'fileinput' '(' file=STRING   (',' type=STRING)? ','?  ')'
        | ('json' '(' json=JSON  ')' )
        | (fileswrap=FILES)
        | (form=FILES)
    )
;

FILES:
    "files" "(" files*=FILETYPE[','] ','? ")"
;

FILETYPE:
    ( '(' name=STRING ',' path=STRING  (',' (type=STRING)?)? ')')
;

TOFILE: 'output' '('output=STRING ')';

JSON:
    array=Array | object=Object
;

Array:
    "[" (values*=Value[','] ) ','? "]"
;

Value:
    multi=MULTILINE_STRING | str=String | var=VarString |  flt=Float | int=Int | bl=Bool | object=Object | array=Array | null="null"
;


Bool:
    value=BOOL
;

Int:
    value=INT
;

Float:
    value=STRICTFLOAT
;

String: 
    value=STRING
;

Object:
    "{" members*=Member[','] ","? "}"
;

Member:
    (multi=MULTILINE_STRING ':' value=Value) |
    (key=STRING ':' value=Value) |
    (var=VarString ':' value=Value)
;
Comment:
    UnixComment | CommentLine | CommentBlock
;
UnixComment:
    /#.*$/
;
CommentLine:
    /\/\/.*?$/
;

CommentBlock:
    /\/\*(.|\n)*?\*\//
;

VarString:
    "{{" /[\w,$,:]+/ ("="  (/\w+/ | STRING))? "}}"
;

DotString:
    STRING | /\w+/
;
// allow only alphanumeric or string in double/single quotes

MULTILINE_STRING:
        /"""[\S\s]*?"""/ | /'''[\S\s]*?'''/

;

URLString:
    STRING | /https\:\/\/(\S)+/
    | /http\:\/\/(\S)+/
;
// allows http or https till space otherwise go for


SLASH:
    '\'
;